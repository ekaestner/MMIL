#!/bin/csh -f
# viewdec -- view dec file (decimated surface vertices) with tksurfer

## initialize variables ##
set name = ()
set hemi = ()
set surf = white
set fname = ""

if($#argv < 2) then
  goto usage_exit;
  exit 1;
endif

## parse and check params ##
goto parse_args;
parse_args_return:

goto check_params;
check_params_return:

###############################################################################

# create tcl script
set pidlist = `ps | grep viewdec`
set pid = $pidlist[1]
set out = tempsurf.tcl.$pid
rm -f $out

echo "set dec $fname" >> $out
echo "read_binary_decimation" >> $out
echo "open_window" >> $out
echo "make_lateral_view" >> $out
echo "redraw" >> $out

tksurfer -$name $hemi $surf -tcl $out
rm $out

exit 0;
###############################################

############--------------##################
parse_args:
set cmdline = ($argv);

set fname = $argv[1]; shift;
set name = $argv[1]; shift;
set hemi = $argv[1]; shift;

while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "-surf":
      if ( $#argv == 0) goto arg1err;
      set surf = $argv[1]; shift;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized. 
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

  if ("$hemi" != "rh" && "$hemi" != "lh") then
    echo "ERROR: hemi must be rh or lh"
    exit 1;
  endif

  if ($#name == 0) then
    goto usage_exit;
  endif

  if ($#fname == 0) then
    goto usage_exit;
  endif

goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo " "
  echo "USAGE: fs_viewdec <fname> <subj> <hemi> [options]"
  echo " "
  echo "Required Arguments:";
  echo "  fname     : dec file name"
  echo "  subj      : subject name"
  echo "  hemi      : hemisphere"
  echo " "
  echo "Optional Arguments:";
  echo "  -surf      surface              : [$surf]"
  echo " "
exit 1;
