#!/bin/csh -f
# surfdip -- generate dip file (surface dipoles) with tksurfer

## initialize variables ##
set name = ()
set hemi = ()
set surf = white
set outstem = ""
set outdir = "."

if($#argv < 2) then
  goto usage_exit;
  exit 1;
endif

## parse and check params ##
goto parse_args;
parse_args_return:

goto check_params;
check_params_return:

###############################################################################

if ($outstem == "") then
  set outfile = $outdir/$hemi'_'$surf'.dip'
else
  set outfile = $outdir/$outstem'_'$hemi'_'$surf'.dip'
endif

# create tcl script
set pidlist = `ps | grep surfdip`
set pid = $pidlist[1]
set out = tempsurf.tcl.$pid
rm -f $out

echo "set dip $outfile" >> $out
echo "write_binary_dipoles" >> $out
echo "exit" >> $out

tksurfer -$name $hemi $surf -tcl $out
rm $out

exit 0;
###############################################

############--------------##################
parse_args:
set cmdline = ($argv);

set name = $argv[1]; shift;
set hemi = $argv[1]; shift;

while( $#argv != 0 )

  set flag = $argv[1]; shift;
  
  switch($flag)

    case "-surf":
      if ( $#argv == 0) goto arg1err;
      set surf = $argv[1]; shift;
      breaksw

    case "-outstem"
      if ( $#argv == 0) goto arg1err;
      set outstem = $argv[1]; shift;
      breaksw

    case "-outdir"
      if ( $#argv == 0) goto arg1err;
      set outdir = $argv[1]; shift;
      breaksw

    default:
      echo ERROR: Flag $flag unrecognized. 
      echo $cmdline
      exit 1
      breaksw
  endsw

end

goto parse_args_return;
############--------------##################

############--------------##################
check_params:

  if ("$hemi" != "rh" && "$hemi" != "lh") then
    echo "ERROR: hemi must be rh or lh"
    exit 1;
  endif

  if ($#name == 0) then
    goto usage_exit;
  endif

goto check_params_return;
############--------------##################

############--------------##################
arg1err:
  echo "ERROR: flag $flag requires one argument"
  exit 1
############--------------##################

############--------------##################
usage_exit:
  echo " "
  echo "USAGE: fs_surfdip <subj> <hemi> [options]"
  echo " "
  echo "Required Arguments:";
  echo "  subj      : subject name"
  echo "  hemi      : hemisphere"
  echo " "
  echo "Optional Arguments:";
  echo "  -surf      surface              : [$surf]"
  echo "  -outstem   output file stem     : [$outstem]"
  echo "  -outdir    output dir           : [$outdir]"
  echo " "
exit 1;
