#!/bin/csh

if ($#argv == 0) then
  echo "ERROR: no batchdir argument supplied"
  exit
endif

set batchdir = ~/batchdirs/$1

if ($#argv > 1) then
  set N = $2
else
  set N = 10
endif

cd $batchdir
mkdir -p pbsout
set joblist = `cat scriptlist.txt`

set touchfile = 'pbsout/.isrunning'
if (-e $touchfile) then
  echo "WARNING: $touchfile exists";
endif
touch $touchfile

foreach job ( $joblist )
  echo 'starting '$job
  if (-e 'pbsout/.finished_'$job) then
    rm 'pbsout/.finished_'$job
  endif
  xmajjob $job >& /dev/null &
  while (1)
    sleep 0.1
    if (-e 'pbsout/.isrunning_'$job || -e 'pbsout/.finished_'$job) then
      break;
    endif
  end
  while (1)
    sleep 0.1
    set flist = `ls pbsout/.isrunning*`
    if ($#flist <= $N) then
      @ njobs = $#flist - 1
      echo 'number of jobs running = '$njobs
      break
    endif
  end
end

mv 'pbsout/.isrunning' 'pbsout/.finished'

